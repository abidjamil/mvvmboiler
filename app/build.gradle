apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'androidx.navigation.safeargs'
apply from: '../config/quality/quality.gradle'
apply plugin: 'com.github.triplet.play'


//TODO uncomment line below after adding fabric api secret and key to fabric.properties
//apply plugin: 'io.fabric'

android {
    compileSdkVersion 28
    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }
    compileOptions.incremental = false

    defaultConfig {
        applicationId 'co.appdev.boilerplate'
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1000
        // Major -> Millions, Minor -> Thousands, Bugfix -> Hundreds. E.g 1.3.72 == 1,003,072
        versionName '0.1.0'
    }
    dataBinding {
        enabled = true
    }

    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }

    signingConfigs {
        // You must set up an environment var before release signing
        // Run: export APP_KEY={password}
        // TODO Add your release keystore in /keystore folder
        release {
            storeFile file('keystore/release.keystore')
            keyAlias 'alias'
            storePassword "$System.env.APP_KEY"
            keyPassword "$System.env.APP_KEY"
        }

        debug {
            storeFile file('keystore/debug.keystore')
            keyAlias 'androiddebugkey'
            storePassword 'android'
            keyPassword 'android'
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release

            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            ext.betaDistributionReleaseNotesFilePath =
                    file('../crashlytics_release_notes.txt').absolutePath
        }

        debug {
            versionNameSuffix " Debug"
            debuggable true
        }
    }

    sourceSets {
        def commonTestDir = 'src/commonTest/java'
        test {
            java.srcDir commonTestDir
        }
        androidTest {
            java.srcDir commonTestDir
        }
    }

    //Needed because of this https://github.com/square/okio/issues/58
    lintOptions {
        warning 'InvalidPackage'
    }
}


dependencies {
    final RETROFIT_VERSION = '2.5.0'
    final OK_HTTP_VERSION = '3.11.0'
    final DAGGER_VERSION = '2.5'
    final ROOM = '2.1.0-beta01'
    final LIFE_CYCLE = '2.0.0'

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation 'androidx.appcompat:appcompat:1.1.0-beta01'
    implementation 'androidx.core:core-ktx:1.2.0-alpha02'
    //constraintLayout
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta2'
    // LiveData & ViewModel
    implementation "androidx.lifecycle:lifecycle-extensions:$LIFE_CYCLE"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$LIFE_CYCLE"
    implementation "androidx.navigation:navigation-fragment-ktx:$LIFE_CYCLE"
    implementation "androidx.navigation:navigation-ui-ktx:$LIFE_CYCLE"
    // Room
    implementation "androidx.room:room-runtime:$ROOM"
    kapt "androidx.room:room-compiler:$ROOM"
    // RxAndroid and RxJava
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.8'

    // Dagger dependencies
    implementation "com.google.dagger:dagger:$DAGGER_VERSION"
    kapt "com.google.dagger:dagger-compiler:$DAGGER_VERSION"

    implementation "com.squareup.retrofit2:retrofit:$RETROFIT_VERSION"
    implementation "com.squareup.retrofit2:converter-gson:$RETROFIT_VERSION"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$RETROFIT_VERSION"
    implementation "com.squareup.okhttp3:logging-interceptor:$OK_HTTP_VERSION"

    implementation("com.github.bumptech.glide:glide:4.8.0") {
        exclude group: "com.android.support"
    }
    implementation('com.crashlytics.sdk.android:crashlytics:2.5.7@aar') {
        transitive = true;
    }

    // Permission handling
    implementation 'com.karumi:dexter:5.0.0'
    implementation 'com.jakewharton.timber:timber:4.7.1'
}
